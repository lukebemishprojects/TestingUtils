pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url = "https://maven.lukebemish.dev/releases/"
        }
    }
}

plugins {
    id 'dev.lukebemish.managedversioning' version '2.0.0-beta.7'
    id 'dev.lukebemish.conventions' version '0.2.0-beta.2'
}

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()

    publishing {
        mavenSnapshot()
        mavenPullRequest()
        mavenRelease()
    }

    gitHubActions {
        release {
            prettyName = 'Release'
            workflowDispatch = true
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                buildCache()
                setupGitUser()
                readOnly = false
                gradlew 'Tag Release', 'tagRelease'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                push()
                mavenRelease('github')
                dependencySubmission()
            }
        }
        snapshot {
            prettyName.set 'Snapshot'
            workflowDispatch.set(true)
            onBranches.add 'main'
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                buildCache()
                cacheReadOnly = false
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                mavenSnapshot('github')
            }
        }
        build_pr {
            prettyName.set 'Build PR'
            pullRequest.set(true)
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                pullRequestArtifact()
            }
        }
        publish_pr {
            prettyName.set 'Publish PR'
            publishPullRequestAction(
                    'github',
                    "dev/lukebemish/testingutils,dev/lukebemish/testingutils/*",
                    'Build PR'
            )
        }
    }
}

dependencyResolutionManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url = "https://maven.lukebemish.dev/releases/"
        }
    }
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'testingutils'

include 'framework'

