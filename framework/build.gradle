plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'dev.lukebemish.testingutils'

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set JavaLanguageVersion.of(17)
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    compileOnly cLibs.bundles.compileonly
    compileOnlyApi cLibs.jetbrains.annotations
    compileOnlyApi cLibs.jspecify
    annotationProcessor cLibs.bundles.annotationprocessor

    api libs.junit.jupiter.api
    implementation libs.junit.platform.launcher
    runtimeOnly libs.junit.jupiter.engine
    runtimeOnly libs.apiguardian
    runtimeOnly libs.junit.platform.reporting

    api platform(libs.junit.bom)
}

tasks.named('compileJava', JavaCompile).configure {
    options.javaModuleMainClass = 'dev.lukebemish.testingutils.framework.Framework'
}

jar {
    manifest {
        attributes([
            'Implementation-Version'   : (version as String),
            'Specification-Version'   : (version as String).split("-")[0],
            'Implementation-Commit-Time': managedVersioning.timestamp.get(),
            'Implementation-Commit': managedVersioning.hash.get()
        ])
    }
}
